<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.6.3 on Tue May 16 08:53:10 2023 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>amespahdbidlsuite_species__define.pro (AmesPAHdbIDLSuite Manual)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="amespahdbidlsuite_species__define.pro (AmesPAHdbIDLSuite Manual)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="container">
      <div class="content">
	<code class="source"><span class="comments">; docformat = 'rst'</span>

<span class="comments">;+</span>
<span class="comments">;</span>
<span class="comments">; Class to hold and access the data and properties in a parsed</span>
<span class="comments">; database.</span>
<span class="comments">;</span>
<span class="comments">; Updated versions of the NASA Ames PAH IR Spectroscopic Database and</span>
<span class="comments">; more information can be found at: `www.astrochemistry.org/pahdb &lt;https://www.astrochemistry.org/pahdb>`.</span>
<span class="comments">;</span>
<span class="comments">; :Examples:</span>
<span class="comments">;   Creating, setting and printing Species info::</span>
<span class="comments">;</span>
<span class="comments">;     IDL> species = OBJ_NEW('AmesPAHdbIDLSuite_Species')</span>
<span class="comments">;     IDL> species->Set,data</span>
<span class="comments">;     IDL> species->Print</span>
<span class="comments">;     IDL> OBJ_DESTROY,info</span>
<span class="comments">;</span>
<span class="comments">; :Uses:</span>
<span class="comments">;    AmesPAHdbIDLSuite_Species,AmesPAHdbIDLSuite_Transitions,AmesPAHdbIDLSuite_Laboratory_Spectrum,AmesPAHdbIDLSuite_Geometry</span>
<span class="comments">;</span>
<span class="comments">; :Author:</span>
<span class="comments">;   Dr. Christiaan Boersma</span>
<span class="comments">;</span>
<span class="comments">; :Copyright:</span>
<span class="comments">;   BSD licensed</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;   Changes::</span>
<span class="comments">;</span>
<span class="comments">;     05-08-2019</span>
<span class="comments">;     In the absence of Michael Galloy's STR_REPLACE function</span>
<span class="comments">;     FormatFormulae will print a message and return. Christiaan</span>
<span class="comments">;     Boersma.</span>
<span class="comments">;     10-17-2017</span>
<span class="comments">;     Fixed units definition in TRANSITIONS.</span>
<span class="comments">;     Christiaan Boersma.</span>
<span class="comments">;     02-01-2015</span>
<span class="comments">;     First version of the file. Christiaan Boersma.</span>
<span class="comments">;</span>
<span class="comments">;-</span>

<span class="comments">;+</span>
<span class="comments">; Make the formulae look pretty by embedding formatting commands.</span>
<span class="comments">;</span>
<span class="comments">; :Uses:</span>
<span class="comments">;   STR_REPLACE by Michael Galloy</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   MARKUP</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species::FormatFormulae

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF FILE_WHICH("str_replace.pro", /INCLUDE_CURRENT_DIR) EQ "" THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"          FORMATFORMULAE REQUIRES STR_REPLACE            "
     PRINT,"========================================================="
     PRINT
     RETURN
  ENDIF

  FOR i = 0, self.nuids - 1 DO BEGIN

     (*self.data)[i].formula = STR_REPLACE((*self.data)[i].formula, "([A-Z])([0-9]+)", "$1!L$2!N", /GLOBAL)

     (*self.data)[i].formula = STR_REPLACE((*self.data)[i].formula, "((\+)+|(\+[0-9])|(-)+|(-[0-9]))", "!U$1!N")
  ENDFOR
END

<span class="comments">;+</span>
<span class="comments">;  Output species description.</span>
<span class="comments">;</span>
<span class="comments">;  :Params:</span>
<span class="comments">;    UID: in, optional, type=long</span>
<span class="comments">;      UID of the species</span>
<span class="comments">;</span>
<span class="comments">;   :Keywords:</span>
<span class="comments">;     Str: out, optional, type="string array"</span>
<span class="comments">;       Ouput to Str</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   INFORMATIVE</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species::Print,UID,Str=Str

  COMPILE_OPT IDL2

  ON_ERROR,2

  nselect1 = self.nuids

  IF N_PARAMS() EQ 0 THEN select1 = ULINDGEN(nselect1) $
  ELSE select1 = WHERE((*self.data).uid EQ UID, nselect1)

  IF nselect1 EQ 0 THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"                     UID NOT FOUND                       "
     PRINT,"========================================================="
     PRINT
     RETURN
  ENDIF

  FOR i = 0, nselect1 - 1 DO BEGIN

     tags = TAG_NAMES((*self.data)[select1[i]])

     ntags = N_TAGS((*self.data)[select1[i]])

     IF KEYWORD_SET(Str) THEN BEGIN

        FOR j = 0, ntags - 1 DO Str = [Str, STRING(FORMAT='(A-10,":",4X,A-0)',tags[j],STRTRIM((*self.data)[select1[i]].(j), 2))]

        IF SIZE(*self.comments, /TNAME) EQ 'STRUCT' THEN BEGIN

           select2 = WHERE((*self.comments).uid EQ (*self.data)[select1[i]].uid, nselect2)

           FOR j = 0, nselect2 - 1 DO Str = [Str, STRING(FORMAT='(A-10,":",4X,A-0)',"COMMENT",STRTRIM((*self.comments)[select2].str, 2))]
        ENDIF

        IF SIZE(*self.references, /TNAME) EQ 'STRUCT' THEN BEGIN

           select2 = WHERE((*self.references).uid EQ (*self.data)[select1[i]].uid, nselect2)

           FOR j = 0, nselect2 - 1 DO Str = [Str, STRING(FORMAT='(A-10,":",4X,A-0)',"REFERENCE",STRTRIM((*self.references)[select2].str, 2))]
        ENDIF
     ENDIF ELSE BEGIN

        PRINT
        PRINT,"========================================================="

        FOR j = 0, ntags - 1 DO PRINT,FORMAT='(A-10,":",4X,A-0)',tags[j],STRTRIM(STRING((*self.data)[select1[i]].(j)), 2)

        IF SIZE(*self.comments, /TNAME) EQ 'STRUCT' THEN BEGIN

           select2 = WHERE((*self.comments).uid EQ (*self.data)[select1[i]].uid, nselect2)

           FOR j = 0, nselect2 - 1 DO PRINT,FORMAT='(A-10,":",4X,A-0)',"COMMENT",STRTRIM((*self.comments)[select2].str, 2)
        ENDIF

        IF SIZE(*self.references, /TNAME) EQ 'STRUCT' THEN BEGIN

           select2 = WHERE((*self.references).uid EQ (*self.data)[select1[i]].uid, nselect2)

           FOR j = 0, nselect2 - 1 DO PRINT,FORMAT='(A-10,":",4X,A-0)',"REFERENCE",STRTRIM((*self.references)[select2].str, 2)
        ENDIF

        PRINT,"========================================================="
        PRINT
     ENDELSE
  ENDFOR

  IF KEYWORD_SET(Str) THEN Str = Str[1:*]
END

<span class="comments">;+</span>
<span class="comments">; Retrieves species comments.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdb_Comment_S array</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Comments

  COMPILE_OPT IDL2

  ON_ERROR,2

  RETURN,*self.comments
END

<span class="comments">;+</span>
<span class="comments">; Retrieves species references.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdb_Reference_S array</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::References

  COMPILE_OPT IDL2

  ON_ERROR,2

  RETURN,*self.references
END

<span class="comments">;+</span>
<span class="comments">; Retrieves species transitions.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Transitions-instance</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Transitions

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF NOT PTR_VALID(self.database) THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"              VALID DATABASE POINTER NEEDED              "
     PRINT,"========================================================="
     PRINT
     self.state = 0
     RETURN, OBJ_NEW()
  ENDIF

  RETURN,OBJ_NEW('AmesPAHdbIDLSuite_Transitions', $
                 Type=(*self.database).type ,$
                 Version=(*self.database).version, $
                 Data=self->GetTagByUID('transitions', *self.uids, Count), $
                 PAHdb=self.database, $
                 Uids=*self.uids, $
                 Model={type:'AMESPAHDBIDLSUITE_MODEL_ZEROKELVIN_S',$
                        Temperature:0D, $
                        description:STRING(FORMAT='(A-11,":",X,A-0)', "model", "ZeroKelvin")}, $
                 Units={AmesPAHdb_Data_Units_S, $
                 abscissa:{AmesPAHdb_Unit_S, $
                    unit:1, $
                    str:'frequency [cm!U-1!N]'}, $
                 ordinate:{AmesPAHdb_Unit_S, $
                    unit:2, $
                    str:'integrated cross-section [km/mol]'}})
END

<span class="comments">;+</span>
<span class="comments">; Retrieves species geometry.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Geometry-instance</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Geometry

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF NOT PTR_VALID(self.database) THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"              VALID DATABASE POINTER NEEDED              "
     PRINT,"========================================================="
     PRINT
     self.state = 0
     RETURN, OBJ_NEW()
  ENDIF

  RETURN,OBJ_NEW('AmesPAHdbIDLSuite_Geometry', $
                 Data=self->GetTagByUID('geometries', *self.uids, Count), $
                 Uids=*self.uids)
END


<span class="comments">;+</span>
<span class="comments">; Retrieves species laboratory spectra.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Laboratory_Spectrum-instance</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Laboratory

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF NOT PTR_VALID(self.database) THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"              VALID DATABASE POINTER NEEDED              "
     PRINT,"========================================================="
     PRINT
     self.state = 0
     RETURN, OBJ_NEW()
  ENDIF

  IF (*self.database).type NE 'experimental' THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"            EXPERIMENTAL DATABASE REQUIRED               "
     PRINT,"========================================================="
     PRINT
     RETURN, OBJ_NEW()
  ENDIF

  RETURN,OBJ_NEW('AmesPAHdbIDLSuite_Laboratory_Spectrum', $
                 Type=(*self.database).type ,$
                 Version=(*self.database).version, $
                 Data=self->GetTagByUID('laboratory', *self.uids, Count), $
                 PAHdb=self.database, $
                 Model={type:'AMESPAHDBIDLSUITE_RAW_LABORATORY_S',$
                        Temperature:0D, $
                        description:STRING(FORMAT='(A-11,":",X,A-0)', "model", "Laboratory")}, $
                 Uids=*self.uids, $
                 Units={AmesPAHdb_Data_Units_S, $
                        x:{AmesPAHdb_Unit_S, $
                           unit:1, $
                           str:'frequency [cm!U-1!N]'}, $
                        y:{AmesPAHdb_Unit_S, $
                           unit:1, $
                           str:'absorbance [-log(I/I!L0!N)]'}})
END

<span class="comments">;+</span>
<span class="comments">; Updates Species to the Intersection with UIDs</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   UIDs: in, required, type="long or long array"</span>
<span class="comments">;     UIDs to consider for Difference</span>
<span class="comments">;   Count: out, optional, type=long</span>
<span class="comments">;     Number of UIDs</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET OPERATIONS</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species::Intersect,UIDs,Count

  COMPILE_OPT IDL2

  ON_ERROR,2

  nuids = N_ELEMENTS(UIDS)

  ndata = N_ELEMENTS(*self.data)

  idx = ULINDGEN(ndata, nuids)

  select = WHERE((*self.data)[idx MOD ndata].uid EQ UIDS[idx / ndata], nselect) MOD ndata

  IF nselect EQ 0 THEN BEGIN
     PRINT
     PRINT,"========================================================="
     PRINT,"                 NO INTERSECTION FOUND                   "
     PRINT,"========================================================="
     PRINT
     self.state = 0
     RETURN
  ENDIF

  *self.data = (*self.data)[select]

  *self.uids = (*self.data)[UNIQ((*self.data).uid, SORT((*self.data).uid))].uid

  self.nuids = N_ELEMENTS(*self.uids)

  Count = self.nuids

  PRINT
  PRINT,"========================================================="
  PRINT,"           INTERSECTION FOUND: "+STRING(FORMAT='(I-0)', Count)
  PRINT,"========================================================="
  PRINT

  ndata = N_ELEMENTS(*self.references)

  select = WHERE((*self.references)[idx MOD ndata].uid EQ UIDS[idx / ndata], nselect) MOD ndata

  IF nselect GT 0 THEN *self.references = (*self.references)[select]

  ndata = N_ELEMENTS(*self.comments)

  select = WHERE((*self.comments)[idx MOD ndata].uid EQ UIDS[idx / ndata], nselect) MOD ndata

  IF nselect GT 0 THEN *self.comments = (*self.comments)[select]

END

<span class="comments">;+</span>
<span class="comments">; Returns the data on associated with the given Tag</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;  struct (variable)</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Tag: in, required, type=string</span>
<span class="comments">;    Name of the Tag</span>
<span class="comments">;   UIDs: in, required, type="long array (1D)"</span>
<span class="comments">;     UIDs to consider; -1 for all</span>
<span class="comments">;   Count: out, optional, type=long</span>
<span class="comments">;     Number of species found</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   RETRIEVAL</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::GetTagByUID,Tag,UIDs,Count

  COMPILE_OPT IDL2

  ON_ERROR,2

  Count = 0

  itag = WHERE(STRLOWCASE(TAG_NAMES((*self.database).data)) EQ Tag, ntag)

  IF ntag NE 1 THEN RETURN,-1

  nuids = N_ELEMENTS(UIDs)

  IF SIZE(UIDs, /DIMENSIONS) EQ 0 AND UIDs EQ -1 THEN BEGIN

     Count = N_ELEMENTS((*self.database).data.(itag))

     UIDs = (*self.database).data.(itag)[UNIQ((*self.database).data.(itag).uid, SORT((*self.database).data.(itag).uid))].uid

     RETURN,(*self.database).data.(itag)
  ENDIF

  nitag = N_ELEMENTS((*self.database).data.(itag))

  idx = ULINDGEN(nitag, nuids)

  select = WHERE((*self.database).data.(itag)[idx MOD nitag].uid EQ UIDs[idx / nitag], Count) MOD nitag

  IF Count EQ 0 THEN RETURN,-1

  UIDs = (*self.database).data.(itag)[select[UNIQ((*self.database).data.(itag)[select].uid, SORT((*self.database).data.(itag)[select].uid))]].uid

  RETURN,(*self.database).data.(itag)[select]
END

<span class="comments">;+</span>
<span class="comments">; Retrieves the AmesPAHdbIDLSuite_Species representation in a</span>
<span class="comments">; structure.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   Structure</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Get

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF NOT PTR_VALID(self.data) THEN RETURN, 0

  RETURN,{type:OBJ_CLASS(self)+'_S', $
          database:self.type, $
          version:self.version, $
          data:*self.data, $
          uids:*self.uids, $
          nuids:self.nuids, $
          references:PTR_VALID(self.references) ? *self.references : PTR_NEW(), $
          comments:PTR_VALID(self.comments) ? *self.comments : PTR_NEW()}
END

<span class="comments">;+</span>
<span class="comments">; Populates the AmesPAHdbIDLSuite_Species-instance.</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Struct: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;   Type: in, optional, type=string</span>
<span class="comments">;     Type of Data</span>
<span class="comments">;   Version: in, optional, type=string</span>
<span class="comments">;    Versioning information</span>
<span class="comments">;   Data: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;   PAHdb: in, optional, type=pointer</span>
<span class="comments">;     Pointer to parsed database file</span>
<span class="comments">;   Uids: in, optional, type="long array (1D)"</span>
<span class="comments">;     UIDs in Data</span>
<span class="comments">;   References: in, optional, type="AmesPAHdb_Reference_S array"</span>
<span class="comments">;     References</span>
<span class="comments">;   Comments: in, optional, type="AmesPAHdb_Comment_S array"</span>
<span class="comments">;     Comments</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species::Set,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,References=References,Comments=Comments

  COMPILE_OPT IDL2, HIDDEN

  ON_ERROR,2

  IF N_PARAMS() GT 0 THEN BEGIN

     tag = WHERE(TAG_NAMES(Struct) EQ 'TYPE', ntype)

     IF ntype EQ 1 THEN BEGIN

        IF Struct.(tag) EQ OBJ_CLASS(self)+'_S' THEN BEGIN

           IF NOT KEYWORD_SET(Type) THEN self.type = Struct.database

           IF NOT KEYWORD_SET(Version) THEN self.version = Struct.version

           IF NOT KEYWORD_SET(Data) THEN BEGIN

              IF PTR_VALID(self.data) THEN PTR_FREE,self.data

              self.data = PTR_NEW(Struct.data)
           ENDIF

           IF NOT KEYWORD_SET(Uids) THEN BEGIN

              IF PTR_VALID(self.uids) THEN PTR_FREE,self.uids

              self.uids = PTR_NEW(Struct.uids)

              self.nuids = N_ELEMENTS(*self.uids)
           ENDIF

           IF NOT KEYWORD_SET(References) THEN BEGIN

              IF PTR_VALID(self.references) THEN PTR_FREE,self.references

              self.references = PTR_NEW(Struct.references)
           ENDIF

           IF NOT KEYWORD_SET(Comments) THEN BEGIN

              IF PTR_VALID(self.comments) THEN PTR_FREE,self.comments

              self.comments = PTR_NEW(Struct.comments)
           ENDIF
        ENDIF
     ENDIF
  ENDIF

  IF KEYWORD_SET(Type) THEN self.type = Type

  IF KEYWORD_SET(Version) THEN self.version = Version

  IF KEYWORD_SET(Data) THEN BEGIN

     IF PTR_VALID(self.data) THEN PTR_FREE,self.data

     self.data = PTR_NEW(Data)
  ENDIF

  IF KEYWORD_SET(PAHdb) THEN self.database = PAHdb

  IF KEYWORD_SET(Uids) THEN BEGIN

     IF PTR_VALID(self.uids) THEN PTR_FREE,self.uids

     self.uids = PTR_NEW(Uids)

     self.nuids = N_ELEMENTS(*self.uids)
  ENDIF

  IF KEYWORD_SET(References) THEN BEGIN

     IF PTR_VALID(self.references) THEN PTR_FREE,self.references

     self.references = PTR_NEW(References)
  ENDIF

  IF KEYWORD_SET(Comments) THEN BEGIN

     IF PTR_VALID(self.comments) THEN PTR_FREE,self.comments

     self.comments = PTR_NEW(Comments)
  ENDIF

  IF PTR_VALID(self.database) THEN BEGIN

     IF STRCMP((*self.database).type, self.type) EQ 0 THEN BEGIN
        PRINT
        PRINT,"========================================================="
        PRINT,"             DATABASE MISMATCH: "+(*self.database).type+" != "+self.type
        PRINT,"========================================================="
        PRINT
        self.state = 0
        RETURN
     ENDIF

     IF STRCMP((*self.database).version, self.version) EQ 0 THEN BEGIN
        PRINT
        PRINT,"========================================================="
        PRINT,"              VERSION MISMATCH: "+(*self.database).version+" != "+self.version
        PRINT,"========================================================="
        PRINT
        self.state = 0
        RETURN
     ENDIF
  ENDIF

  self.state = 1
END

<span class="comments">;+</span>
<span class="comments">; Clean-up an AmesPAHdbIDLSuite_Species-instance</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species::Cleanup

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF PTR_VALID(self.data) THEN PTR_FREE,self.data

  IF PTR_VALID(self.uids) THEN PTR_FREE,self.uids

  IF PTR_VALID(self.references) THEN PTR_FREE,self.references

  IF PTR_VALID(self.comments) THEN PTR_FREE,self.comments
END

<span class="comments">;+</span>
<span class="comments">; Create an AmesPAHdbIDLSuite_Species-instance</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Species-instance</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Struct: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;   Type: in, optional, type=string</span>
<span class="comments">;     Type of Data</span>
<span class="comments">;   Version: in, optional, type=string</span>
<span class="comments">;    Versioning information</span>
<span class="comments">;   Data: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;   PAHdb: in, optional, type=pointer</span>
<span class="comments">;     Pointer to parsed database file</span>
<span class="comments">;   Uids: in, optional, type="long array (1D)"</span>
<span class="comments">;     UIDs in Data</span>
<span class="comments">;   References: in, optional, type="AmesPAHdb_Reference_S array"</span>
<span class="comments">;     References</span>
<span class="comments">;   Comments: in, optional, type="AmesPAHdb_Comment_S array"</span>
<span class="comments">;     Comments</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Species::Init,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,References=References,Comments=Comments

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF N_PARAMS() GT 0 THEN self->Set,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,References=References,Comments=Comments $
  ELSE self->Set,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,References=References,Comments=Comments

 RETURN, self.state
END

<span class="comments">;+</span>
<span class="comments">; Defines the AmesPAHdbIDLSuite_Species Class</span>
<span class="comments">;</span>
<span class="comments">; :Fields:</span>
<span class="comments">;   state: type=int</span>
<span class="comments">;     Internal state</span>
<span class="comments">;   type: type=string</span>
<span class="comments">;     Type of Data</span>
<span class="comments">;   version: type=sting</span>
<span class="comments">;     Versioning information</span>
<span class="comments">;   data: type=pointer</span>
<span class="comments">;     Data pointer</span>
<span class="comments">;   database: type=pointer</span>
<span class="comments">;     Database pointer</span>
<span class="comments">;   uids: type=pointer</span>
<span class="comments">;     Pointer to the UIDs of Data</span>
<span class="comments">;   nuids: type=long</span>
<span class="comments">;     Number of UIDs</span>
<span class="comments">;   references: type=pointer</span>
<span class="comments">;     Pointer to the references</span>
<span class="comments">;   comments: type=pointer</span>
<span class="comments">;     Pointer to the comments</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Species__DEFINE

  COMPILE_OPT IDL2

  ON_ERROR,2

  void = {AmesPAHdbIDLSuite_Species, $
          state:0L, $
          type:'', $
          version:'', $
          data:PTR_NEW(), $
          database:PTR_NEW(), $
          uids:PTR_NEW(), $
          nuids:0L, $
          references:PTR_NEW(), $
          comments:PTR_NEW()}
END

<span class="comments">; END OF amespahdbidlsuite_species__define.pro</span>
</code>
      </div>
    </div>
  </body>
</html>