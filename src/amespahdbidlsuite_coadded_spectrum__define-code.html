<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.6.3 on Tue Apr  9 12:19:02 2024 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>amespahdbidlsuite_coadded_spectrum__define.pro (AmesPAHdbIDLSuite Manual)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="amespahdbidlsuite_coadded_spectrum__define.pro (AmesPAHdbIDLSuite Manual)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="container">
      <div class="content">
	<code class="source"><span class="comments">; docformat = 'rst'</span>

<span class="comments">;+</span>
<span class="comments">;</span>
<span class="comments">; Class to manage a co-added spectrrum.</span>
<span class="comments">;</span>
<span class="comments">; Updated versions of the NASA Ames PAH IR Spectroscopic Database and</span>
<span class="comments">; more information can be found at: `www.astrochemistry.org/pahdb &lt;https://www.astrochemistry.org/pahdb>`.</span>
<span class="comments">;</span>
<span class="comments">; :Examples:</span>
<span class="comments">;   Create and destroy an</span>
<span class="comments">;   AmesPAHdbIDLSuite_CoAdded_Spectrum-instance::</span>
<span class="comments">;</span>
<span class="comments">;     IDL> coadded = OBJ_NEW('AmesPAHdbIDLSuite_Coadded_Spectrum')</span>
<span class="comments">;     IDL> OBJ_DESTROY,coadded</span>
<span class="comments">;</span>
<span class="comments">; :Author:</span>
<span class="comments">;   Dr. Christiaan Boersma</span>
<span class="comments">;</span>
<span class="comments">; :Copyright:</span>
<span class="comments">;   BSD licensed</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;   Changes::</span>
<span class="comments">;</span>
<span class="comments">;     04-30-2021</span>
<span class="comments">;     Remove duplicated SET and CLEANUP methods. Christiaan Boersma.</span>
<span class="comments">;     12-13-2018</span>
<span class="comments">;     Now writing files as IPAC-tables. Christiaan Boersma.</span>
<span class="comments">;     05-03-2015</span>
<span class="comments">;     Updated weights formatting for DESCRIPTION. Christiaan Boersma.</span>
<span class="comments">;     02-01-2015</span>
<span class="comments">;     First version of the file. Christiaan Boersma.</span>
<span class="comments">;-</span>

<span class="comments">;+</span>
<span class="comments">;  Output spectrum description.</span>
<span class="comments">;</span>
<span class="comments">;  :Params:</span>
<span class="comments">;    Str: out, optional, type="string array"</span>
<span class="comments">;      Ouput to Str</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   INFORMATIVE</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum::Description,Str

  COMPILE_OPT IDL2

  ON_ERROR,2

  self->AmesPAHdbIDLSuite_Spectrum::Description,Str

  Str = [Str, STRING(FORMAT='(A-12,":",X,I-0)', "n", self.nuids)]

  Str = [Str, STRING(FORMAT='(A-12,":",X,A-0)', "weighted", PTR_VALID(self.weights) ? "yes" : "no")]

  IF PTR_VALID(self.weights) THEN BEGIN

     weights_str = STRTRIM(STRING(FORMAT='(g-7.2)', (*self.weights)[0].weight), 2)

     nweights = N_ELEMENTS((*self.weights))

     IF nweights GT 4 THEN weights = (*self.weights)[1:2].weight $
     ELSE weights = (*self.weights).weight

     IF nweights GT 1 THEN weights_str += STRJOIN(STRTRIM(STRING(FORMAT='(",!C|",13X,g-7.2)', weights), 2), "")

     IF nweights GT 2 THEN weights_str += STRING(FORMAT='("!C|",13X,A0)', "...")

  ENDIF ELSE weights_str = '1.0'

  Str = [Str, STRING(FORMAT='(A-12,":",X,A-0)', "|_weights", weights_str)]

  Str = [Str, STRING(FORMAT='(A-12,":",X,A-0)', "|_averaged", self.averaged ? "yes" : "no")]

  Str = STRJOIN(Str, "!C")

  IF N_PARAMS() GT 0 THEN RETURN

  PRINT,STRJOIN(STRSPLIT(Str, "!C", /EXTRACT, /REGEX), STRING(10B))
END

<span class="comments">;+</span>
<span class="comments">;  Plot the coadded spectrum.</span>
<span class="comments">;</span>
<span class="comments">;  :Keywords:</span>
<span class="comments">;    Wavelength: in, optional, type=int</span>
<span class="comments">;      Whether to set the abscissa units to wavelength</span>
<span class="comments">;    Stick: in, optional, type=int</span>
<span class="comments">;      Whether to plot the spectrum as sticks</span>
<span class="comments">;    Fill: in, optional, type=int</span>
<span class="comments">;       Whether to solid-fill the spectrum</span>
<span class="comments">;    Oplot: in, optional, type=int</span>
<span class="comments">;      Whether to over-plot the spectrum on a previous defined plot</span>
<span class="comments">;    Legend: in, optional, type=int, default=1B</span>
<span class="comments">;      Whether to show a legend</span>
<span class="comments">;    Color: in, optional, type=int</span>
<span class="comments">;      Color to plot the spectrum with</span>
<span class="comments">;    _EXTRA: in, optional, type=struct</span>
<span class="comments">;      Required for IDL's keyword-inheritance mechanism</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   PLOTTING</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum::Plot,Wavelength=Wavelength,Stick=Stick,Fill=Fill,Oplot=Oplot,Legend=Legend,Color=Color,_EXTRA=EXTRA

  COMPILE_OPT IDL2

  ON_ERROR,2

  self->AmesPAHdbIDLSuite_Plot::Setup,Oplot=Oplot,XSIZE=600,YSIZE=400

  x = *self.grid

  xunits = self.units.abscissa.str

  xrange = [MAX(x, MIN=xmin), xmin]

  IF KEYWORD_SET(Wavelength) THEN BEGIN

     x = 1D4 / x

     xrange = [MIN(x, MAX=xmax), xmax]

     xunits = 'wavelength [!Mm!Xm]'
  ENDIF

  IF NOT KEYWORD_SET(Oplot) THEN self->AmesPAHdbIDLSuite_Plot::Plot,x,(*self.data).intensity,Color=Color,XRANGE=xrange,XTITLE=xunits,YTITLE=self.units.ordinate.str,/NoData,_EXTRA=EXTRA

  IF NOT KEYWORD_SET(Color) THEN Color = 2

  self->AmesPAHdbIDLSuite_Plot::Oplot,x,(*self.data).intensity,Stick=Stick,Fill=Fill,COLOR=Color

  IF SIZE(Legend, /TYPE) EQ 0 THEN Legend = 1

  IF Legend THEN BEGIN

     self->Description,outs

     self->AmesPAHdbIDLSuite_Plot::Legend,outs
  ENDIF

  self->AmesPAHdbIDLSuite_Plot::Restore
END

<span class="comments">;+</span>
<span class="comments">; Write the co-added spectrum to file as an IPAC-table.</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Filename: in, optional, type=string</span>
<span class="comments">;     Output filename</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   OUTPUT</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum::Write,Filename

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF N_PARAMS() LT 1 THEN Filename = OBJ_CLASS(self) + '.tbl'

  timestamp = SYSTIME()
  hdr = []
  FXADDPAR,hdr,"DATE",timestamp," Date this file was generated"
  FXADDPAR,hdr,"ORIGIN","NASA Ames Research Center"," Organization generating this file"
  FXADDPAR,hdr,"CREATOR",STRING(FORMAT='("IDL",X,A0,X,"on",X,A0)', !VERSION.RELEASE, !VERSION.OS_NAME)," Software used to create this file"
  FXADDPAR,hdr,"SOFTWARE","AmesPAHdbIDLSuite"," Program used to create this file"
  FXADDPAR,hdr,"AUTHOR","Dr. C. Boersma"," Author of the program"
  FXADDPAR,hdr,"TYPE",OBJ_CLASS(self)," AmesPAHdbIDLSuite data type"

  self->Description,description

  comments = STRSPLIT(description, "!C", /EXTRACT, /REGEX, COUNT=ncomments)

  FOR i = 0L, ncomments - 1 DO FXADDPAR,hdr,"COMMENT",comments[i]

  IF PTR_VALID(self.weights) THEN BEGIN

     srt = SORT((*self.weights).uids)

     FXADDPAR,hdr,"COMMENT",STRING(FORMAT='(A-12,":",X,'+STRING(self.nuids)+'(G-10.3,","))',"WEIGHTS:",(*self.weights)[srt].weight)
  ENDIF ELSE BEGIN

     srt = SORT(*self.uids)

     FXADDPAR,hdr,"COMMENT",STRING(FORMAT='(A-12,":",X,'+STRING(self.nuids)+'(I0,","))',"UIDs",(*self.uids)[srt])
  ENDELSE

  abscissa = STREGEX(self.units.abscissa.str, '(.*) \[(.*)\]', /SUBEXPR, /EXTRACT)

  ordinate = STREGEX(self.units.ordinate.str, '(.*) \[(.*)\]', /SUBEXPR, /EXTRACT)

  half_abscissa_len = STRLEN(abscissa[1]) / 2
  half_ordinate_len = STRLEN(ordinate[1]) / 2

  fmt1 = '("|",A' + STRING(FORMAT='(I0)', 12 + half_abscissa_len) + ',' + STRING(FORMAT='(I0)', 13 - half_abscissa_len) + 'X,' + $
          '"|",A' + STRING(FORMAT='(I0)', 12 + half_ordinate_len) + ',' + STRING(FORMAT='(I0)', 13 - half_ordinate_len) + 'X,' + $
          '"|")'

  fmt2 = '("|",A' + STRING(FORMAT='(I0)', 12 + 3) + ',' + STRING(FORMAT='(I0)', 13 - 3) + 'X,' + $
          '"|",A' + STRING(FORMAT='(I0)', 12 + 3) + ',' + STRING(FORMAT='(I0)', 13 - 3) + 'X,' + $
          '"|")'

  half_abscissa_len = STRLEN(abscissa[2]) / 2
  half_ordinate_len = STRLEN(ordinate[2]) / 2

  fmt3 = '("|",A' + STRING(FORMAT='(I0)', 12 + half_abscissa_len) + ',' + STRING(FORMAT='(I0)', 13 - half_abscissa_len) + 'X,' + $
          '"|",A' + STRING(FORMAT='(I0)', 12 + half_ordinate_len) + ',' + STRING(FORMAT='(I0)', 13 - half_ordinate_len) + 'X,' + $
          '"|")'

  cols = [STRING(FORMAT=fmt1,STRUPCASE(abscissa[1]),STRUPCASE(ordinate[1])), $
          STRING(FORMAT=fmt2,"double","double"), $
          STRING(FORMAT=fmt3,abscissa[2],ordinate[2])]

  OPENW,funit,Filename,/GET_LUN
  PRINTF,funit,FORMAT='("\",A0)',hdr[0:WHERE(STRPOS(hdr, 'END') EQ 0)]
  PRINTF,funit,STRJOIN(cols, STRING( 10B ))
  n = N_ELEMENTS(*self.grid)
  FOR i = 0L, n - 1 DO PRINTF,funit,FORMAT='(X,F25.6,X,F25.6)',(*self.grid)[i],(*self.data).intensity[i]
  CLOSE,funit
  FREE_LUN,funit

  PRINT
  PRINT,"========================================================="
  PRINT,"    WRITTEN IPAC TABLE: ", Filename
  PRINT,"========================================================="
  PRINT
END

<span class="comments">;+</span>
<span class="comments">; Retrieves the AmesPAHdbIDLSuite_CoAdded_Spectrum representation in a</span>
<span class="comments">; structure.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   Structure</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Coadded_Spectrum::Get

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF NOT PTR_VALID(self.data) THEN RETURN, 0

  struct = self->AmesPAHdbIDLSuite_Spectrum::Get()

  struct.type = OBJ_CLASS(self)+'_S'

  struct = CREATE_STRUCT(struct, 'averaged', self.averaged)

  IF PTR_VALID(self.weights) THEN struct = CREATE_STRUCT(struct, 'weights', *self.weights)

  RETURN,struct
END

<span class="comments">;+</span>
<span class="comments">; Populates the AmesPAHdbIDLSuite_CoAdded_Spectrum-instance.</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Coadded_Spectrum-instance</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Struct: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;   Type: in, optional, type=string</span>
<span class="comments">;     Type of Data</span>
<span class="comments">;   Version: in, optional, type=string</span>
<span class="comments">;    Versioning information</span>
<span class="comments">;   Data: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;   PAHdb: in, optional, type=pointer</span>
<span class="comments">;     Pointer to parsed database file</span>
<span class="comments">;   Uids: in, optional, type="long array (1D)"</span>
<span class="comments">;     UIDs in Data</span>
<span class="comments">;   Model: in, optional, type=string</span>
<span class="comments">;     References</span>
<span class="comments">;   Units: in, optional, type="AmesPAHdb_Units_S struct"</span>
<span class="comments">;     Comments</span>
<span class="comments">;   Shift: in, optional, type=float</span>
<span class="comments">;     Shift</span>
<span class="comments">;   Grid: in, optional, type="float array"</span>
<span class="comments">;     Grid</span>
<span class="comments">;   Profile: in, optional, type=string</span>
<span class="comments">;     Profile</span>
<span class="comments">;   FWHM: in, optional, type=float</span>
<span class="comments">;     FWHM</span>
<span class="comments">;   Weights: in, optional, type=struct</span>
<span class="comments">;     Weights</span>
<span class="comments">;   Averaged: in, optional, type=int</span>
<span class="comments">;     Whether the spectra were averaged</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   SET/GET</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum::Set,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM,Weights=Weights,Averaged=Averaged

  IF N_PARAMS() GT 0 THEN BEGIN

     tags = TAG_NAMES(Struct)

     tag = WHERE(tags EQ 'TYPE', ntype)

     IF ntype EQ 1 THEN BEGIN

        IF Struct.(tag) EQ OBJ_CLASS(self)+'_S' THEN BEGIN

           tag = WHERE(tags EQ 'WEIGHTS', nweights)

           IF NOT KEYWORD_SET(Weights) AND nweights EQ 1 THEN BEGIN

              IF PTR_VALID(weights) THEN PTR_FREE,self.weights

              self.weights = PTR_NEW(Struct.(tag))
           ENDIF

           IF NOT KEYWORD_SET(Averaged) THEN self.averaged = Struct.averaged
        ENDIF

        self->AmesPAHdbIDLSuite_Spectrum::Set,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM
     ENDIF
  ENDIF ELSE self->AmesPAHdbIDLSuite_Spectrum::Set,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM

  IF KEYWORD_SET(Weights) THEN BEGIN

     IF PTR_VALID(self.weights) THEN PTR_FREE,self.weights

     self.weights = PTR_NEW(Weights)
  ENDIF

  IF KEYWORD_SET(Averaged) THEN self.averaged = Averaged
END

<span class="comments">;+</span>
<span class="comments">; Clean-up an AmesPAHdbIDLSuite_Coadded_Spectrum-instance</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum::Cleanup

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF PTR_VALID(self.weights) THEN PTR_FREE,self.weights

  self->AmesPAHdbIDLSuite_Spectrum::Cleanup
END

<span class="comments">;+</span>
<span class="comments">; Create an AmesPAHdbIDLSuite_Coadded_Spectrum-instance</span>
<span class="comments">;</span>
<span class="comments">; :Returns:</span>
<span class="comments">;   AmesPAHdbIDLSuite_Data-instance</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;   Struct: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;   Type: in, optional, type=string</span>
<span class="comments">;     Type of Data</span>
<span class="comments">;   Version: in, optional, type=string</span>
<span class="comments">;    Versioning information</span>
<span class="comments">;   Data: in, optional, type=struct</span>
<span class="comments">;     Data structure</span>
<span class="comments">;   PAHdb: in, optional, type=pointer</span>
<span class="comments">;     Pointer to parsed database file</span>
<span class="comments">;   Uids: in, optional, type="long array (1D)"</span>
<span class="comments">;     UIDs in Data</span>
<span class="comments">;   Model: in, optional, type=string</span>
<span class="comments">;     References</span>
<span class="comments">;   Units: in, optional, type="AmesPAHdb_Units_S struct"</span>
<span class="comments">;     Comments</span>
<span class="comments">;   Shift: in, optional, type=float</span>
<span class="comments">;     Shift</span>
<span class="comments">;   Grid: in, optional, type="float array"</span>
<span class="comments">;     Grid</span>
<span class="comments">;   Profile: in, optional, type=string</span>
<span class="comments">;     Profile</span>
<span class="comments">;   FWHM: in, optional, type=float</span>
<span class="comments">;     FWHM</span>
<span class="comments">;   Weights: in, optional, type=struct</span>
<span class="comments">;     Weights</span>
<span class="comments">;   Averaged: in, optional, type=int</span>
<span class="comments">;     Whether the spectra were averaged</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;-</span>
FUNCTION AmesPAHdbIDLSuite_Coadded_Spectrum::Init,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM,Weights=Weights,Averaged=Averaged

  COMPILE_OPT IDL2

  ON_ERROR,2

  IF N_PARAMS() GT 0 THEN self->Set,Struct,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM,Weights=Weights,Averaged=Averaged $
  ELSE self->Set,Type=Type,Version=Version,Data=Data,PAHdb=PAHdb,Uids=Uids,Model=Model,Units=Units,Shift=Shift,Grid=Grid,Profile=Profile,FWHM=FWHM,Weights=Weights,Averaged=Averaged

  RETURN,self.state
END

<span class="comments">;+</span>
<span class="comments">; Defines the AmesPAHdbIDLSuite_Coadded_Spectrum Class</span>
<span class="comments">;</span>
<span class="comments">; :Fields:</span>
<span class="comments">;   weights: type=pointer</span>
<span class="comments">;    Pointer to an array of weights structure</span>
<span class="comments">;   averaged: type=int</span>
<span class="comments">;     Whether the spectra were averaged</span>
<span class="comments">;</span>
<span class="comments">; :Categories:</span>
<span class="comments">;   CLASS</span>
<span class="comments">;</span>
<span class="comments">; :Private:</span>
<span class="comments">;-</span>
PRO AmesPAHdbIDLSuite_Coadded_Spectrum__DEFINE

  COMPILE_OPT IDL2

  ON_ERROR,2

  void = {AmesPAHdbIDLSuite_Coadded_Spectrum, $
          INHERITS AmesPAHdbIDLSuite_Spectrum, $
          weights:PTR_NEW(), $
          averaged:0}
END

<span class="comments">; END OF amespahdbidlsuite_coadded_spectrum__define.pro</span>
</code>
      </div>
    </div>
  </body>
</html>